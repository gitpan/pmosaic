#!/usr/bin/perl -w
#	(C) Simon Drabble	2004-06-19
#	gimp@thebigmachine.org
#	$Id: photomean,v 1.4 2004/07/12 15:05:11 simon Exp $
#


use Gimp ":auto";
use Gimp::Fu;

use strict;

sub photomean_run
{
	my ($file) = @_;
	return unless $file;
	my $img = gimp_file_load($Gimp::Fu::run_mode, $file, $file);
	die "Couldn't load $file" unless $img;
	my $drw = $img->get_active_layer();
	if (!$drw) {
		$drw = ($img->get_layers)[0];
	}

	my $wd = $drw->width;
	my $ht = $drw->height;

	if ($img->base_type() eq GRAY) {
		$img->convert_rgb();
	}
	my $r = $drw->histogram(RED_LUT,   0, 255);
	my $g = $drw->histogram(GREEN_LUT, 0, 255);
	my $b = $drw->histogram(BLUE_LUT,  0, 255);
	my $pv = ($r << 16) + ($g << 8) + ($b);
	printf "$file:#%06x\n", $pv;

	$img->delete; # dispose of the loaded image
	return;
}


register
	"perl_fu_photomean",                                 # fill in name
	"Calculates the average pixel value of an image.",   # a small description
	qq{Calculates the average pixel value of an image, suitable for use in
PhotoMosaic software (pmosaic). The value is written to stdout along with
the image filename.
Images should be RGB or RGB-A (although alpha is not taken into account),
and flattened for best results.
}, # a help text
	"Simon Drabble <gimp\@thebigmachine.org>",         # Your name
	"(c) Simon Drabble <gimp\@thebigmachine.org>",     # Your copyright
	"photomean 2004/06/19",                            # Date
	"<Toolbox>/Xtns/Perl-Fu/photomean",                   # menu path
	"*",                                               # Image types
	[
		[PF_FILE,   "file",          "File",                  ""],
	],
	\&photomean_run;

exit main();


__END__


=head1 NAME

photomean - GIMP plug-in to calculate average pixel-value for an image. Used
in the production of photomosaic pictures.

=head1 SYNOPSIS

Copy to your GIMP plug-ins directory. Run as either

	/path/to/photomean -file /path/to/imagefile 

or from the Toolbox/Xtns/Perl-Fu menu within GIMP.

=head1 ABSTRACT

This script calculates the average pixel value of an image. The image should
be in RGB or RGB-A format - greyscale images are first converted. Only the
active layer of the image will be used.

The pixel value is output to stdout along with the filename, and this can be
used verbatim as an entry in a library index file for pmosaic, which should
accompany this software.

=head1 SEE ALSO

pmosaic, GIMP::Fu

=head1 AUTHOR

Simon Drabble, E<lt>gimp@thebigmachine.orgE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2004  Simon Drabble

This script is free software; you may redistribute it and/or modify 
it under the same terms as Perl itself.

=cut
